import("//brave/components/common/typescript.gni")
import("//build/config/locales.gni")

transpile_web_ui("brave_wallet") {
  # Need this to fire re-pack if changed, nevertheless extension is repacked on each 2nd build
  # what is the output bundle called and what is the entry point file
  entry_points = [
    [
      "brave_wallet",
      rebase_path("braveWalletPanel.tsx"),
    ],
  ]

  # what is the directory / pack name
  resource_name = "brave_wallet"

  # Must match the relative path from the static GRD to the manifest.json
  # plus any other relative path we want these files to live in the extension
  extra_relative_path = "/brave_wallet/out"

  public_asset_path = "/out/"
}

if (is_mac) {
  group("brave_wallet_framework_bundle_data") {
    deps = []
    foreach(locale, locales) {
      deps += [ ":brave_wallet_framework_bundle_data_${locale}" ]
    }
  }

  foreach(locale, locales) {
    bundle_data("brave_wallet_framework_bundle_data_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [ "//brave/components/brave_wallet/resources/extension/brave_wallet/_locales/$source_locale/messages.json" ]
      outputs = [ "{{bundle_resources_dir}}/brave_wallet/_locales/$locale/{{source_file_part}}" ]
    }
  }
} else {
  group("locales") {
    deps = []
    foreach(locale, locales) {
      deps += [ ":locales_${locale}" ]
    }
  }
  foreach(locale, locales) {
    copy("locales_${locale}") {
      locale = string_replace(locale, "-", "_")
      source_locale = string_replace(locale, "nb", "no")
      sources = [ "//brave/components/brave_wallet/resources/extension/brave_wallet/_locales/${source_locale}/messages.json" ]
      outputs = [ "$root_out_dir/resources/brave_wallet/_locales/${locale}/{{source_file_part}}" ]
    }
  }
}
